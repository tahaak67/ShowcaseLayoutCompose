apply plugin: 'maven-publish'

apply plugin: 'signing'

task androidSourcesJar(type: Jar) {

    archiveClassifier.set('sources')

    if (project.plugins.findPlugin("com.android.library")) {

        // For Android libraries

        from android.sourceSets.main.java.srcDirs

        from android.sourceSets.main.kotlin.srcDirs

    } else {

        // For pure Kotlin libraries, in case you have them

        from sourceSets.main.java.srcDirs

        from sourceSets.main.kotlin.srcDirs

    }

}


artifacts {

    archives androidSourcesJar

}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {

    publishing {

        publications {

            release(MavenPublication) {

                // The coordinates of the library, being set from variables that

                // we'll set up later

                groupId PUBLISH_GROUP_ID

                artifactId PUBLISH_ARTIFACT_ID

                version PUBLISH_VERSION


                // Two artifacts, the `aar` (or `jar`) and the sources

                if (project.plugins.findPlugin("com.android.library")) {

                    from components.release

                } else {

                    from components.java

                }


                artifact androidSourcesJar

               // artifact javadocJar


                // Mostly self-explanatory metadata

                pom {

                    name = PUBLISH_ARTIFACT_ID

                    description = 'Showcase Layout allows you to easily showcase and explain compose UI elements to users in a beautiful and attractive way.'

                    url = 'https://github.com/tahaak67/ShowcaseLayoutCompose'

                    licenses {

                        license {

                            name = 'The Apache Software License, Version 2.0'

                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'

                        }

                    }

                    developers {

                        developer {

                            id = 'tahaak67'

                            name = 'Taha Ben Ashur'

                            email = 'dev@tahaben.com.ly'

                        }

                        // Add all other devs here...

                    }


                    // Version control info - if you're using GitHub, follow the

                    // format as seen here

                    scm {

                        connection = 'scm:git:github.com/tahaak67/ShowcaseLayoutCompose.git'

                        developerConnection = 'scm:git:ssh://github.com/tahaak67/ShowcaseLayoutCompose.git'

                        url = 'https://github.com/tahaak67/ShowcaseLayoutCompose/tree/main'

                    }

                }

            }

        }

    }

}

signing {

    useInMemoryPgpKeys(

            rootProject.ext["signingKeyId"],

            rootProject.ext["signingKey"],

            rootProject.ext["signingPassword"],

    )

    sign publishing.publications

}
